DISTRO = "strbo-main"
DISTRO_NAME = "StrBo (T+A Streaming Board)"
DISTRO_CODENAME ?= "${DISTRO_LINE}"
DISTRO_VERSION ?= "0.0.0"
DISTRO_FEATURES = "alsa argp ext2 largefile polkit systemd usbhost wifi xattr"
VIRTUAL-RUNTIME_login_manager = "shadow"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"
VIRTUAL-RUNTIME_base-utils = ""
DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit"
DISTRO_FEATURES_remove = " opengl pulseaudio bluez5"

MAINTAINER = "T+A elektroakustik GmbH & Co. KG <e-service@ta-hifi.com>"

hostname_pn-base-files := "ta-streaming-board"

LOCALCONF_VERSION = "2"
LAYER_CONF_VERSION ?= "8"

SANITY_TESTED_DISTROS ?= " \
            ubuntu-14.04 \n \
            ubuntu-16.04 \n \
            ubuntu-18.04 \n \
            ubuntu-20.04 \n \
            "

HAVE_DEVELOPER_ACCOUNT = "yes"

KERNEL_IMAGETYPE = "uImage"
MACHINE_EXTRA_RRECOMMENDS += " kernel-devicetree"
PREFERRED_PROVIDER_virtual/kernel = "linux-strbo-main"

PREFERRED_VERSION_gerbera = "1.3.5"

SYSTEMD_DEFAULT_TARGET = "strbo.target"

# use SHA256 checksums instead of MD5 in package files, and use GPG for
# signature verification
PACKAGECONFIG_append_pn-opkg = " sha256 gpg"
PACKAGECONFIG_append_pn-opkg-native = " sha256"

# read out systemd journal via HTTP (disabled by default)
PACKAGECONFIG_append_pn-systemd = " microhttpd"

# some extra plugins for GStreamer
PACKAGECONFIG_remove_pn-gstreamer1.0-plugins-base = "opengl egl gles2"
PACKAGECONFIG_append_pn-gstreamer1.0-plugins-bad = "hls libmms rtmp faad opus modplug"
PACKAGECONFIG_append_pn-gstreamer1.0-plugins-good = "wavpack"

# pimp up gdb
EXTRA_OECONF_append_pn-gdb-cross-canadian-arm = "--enable-tui --enable-x"
EXTRA_OECONF_append_pn-gdb-cross-arm = "--enable-tui --enable-x"

IMAGE_FSTYPES = "tar.xz"

OELAYOUT_ABI = "8"
INHERIT += "poky-sanity"
WARN_TO_ERROR_QA = "already-stripped compile-host-path install-host-path \
                    installed-vs-shipped ldflags pn-overrides rpaths staticdev \
                    unknown-configure-option useless-rpaths"
WARN_QA_remove = "${WARN_TO_ERROR_QA}"
ERROR_QA_append = " ${WARN_TO_ERROR_QA}"

# Optionally override variables locally
include strbo-main-override.conf
